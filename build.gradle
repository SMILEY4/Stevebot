group = "stevebot"
version = "dev"

buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://jitpack.io"
        }
    }
}

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2768" // the forge-version we want to use
    runDir = "run"
    mappings = "snapshot_20171003"
}

configurations {
    provided
    compile.extendsFrom(provided)
}

dependencies {
    provided group: 'com.github.SMILEY4', name: 'SimpleMath', version: 'master-SNAPSHOT'
    testImplementation("org.assertj:assertj-core:3.11.1")
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.5.2"
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.5.2')
}

jar {
    from configurations.provided.asFileTree.files.collect { zipTree(it) }
}

test {
    useJUnitPlatform()
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}


